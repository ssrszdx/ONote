# 函数与栈

前一段时间看Joel On Software中关于本科的计算机教育有一段非常精彩的论述：

> I have never met anyone who can do Scheme, Haskell, and C pointers who can't pick up Java in two days, and create better Java code than people with five years of experience in Java, but try explaining that to the average HR drone.
>
> CS is proofs (recursion), algorithms (recursion), languages (lambda calculus), operating systems (pointers), compilers (lambda calculus) -- and so the bottom line is that a Java School that won't teach C and won't teach Scheme is not really teaching computer science, either.

为了更快的开发效率我们往往会使用抽象层级更高的技术，包括语言，框架，设计模式等。所以导致很多程序员包括我自己在内对于底层和基础的知识都会有些生疏和，但是正是这些底层的东西构建了我们熟知的解决方案，同时决定了一个技术人员的上限。 这篇文章除了给自己整理一下知识脉络以外还希望可以引起其他开发者也认识到底层知识的重要性。文中会使用Linux汇编相关的指令以及寄存器，希望读者在看之前能够熟悉这一部分的知识，好吧，我们开始吧！

## 文章脉络

这篇文章主要讲述了函数与栈之间的关系，之所以选择这个点切入有两点原因：

1. 实际开发的过程中，函数是处理复杂度与逻辑的单元。
2. C语言以及一些常用的高级语言(java,c++)都是面相栈的编程语言，这部分牵涉到内存，寄存器等体系结构知识。

通过学习栈和函数之间的关系，我们不仅对于体系结构部分的知识能够得到巩固，同时对其他高级语言在函数调用的部分有一个更深入的了解。

文章分为以下五个部分：

1. 什么是栈
2. 函数的组成部分
3. 函数与栈
4. 递归与栈
5. 总结与扩展

## 什么是栈

这篇文章中的"栈"不是数据结构中经常能遇到的栈，更准确的说法应该是([Call stack - Wikipedia](https://link.zhihu.com/?target=https%3A//en.wikipedia.org/wiki/Call_stack)) 。 Call Stack利用了栈的LIFO(后进先出)的特性来实现函数的调用和返回。后文中你会看到相应的实例，希望读者不要混淆，接下来的文章中我们统一用栈来指代Call Stack.下面我们来讲一下栈。

基本上可执行程序由4个区域组成:

* 代码段：运行程序时包含的机器指令。
* 静态数据区：程序生命周期内一直存在的数据，全局变量和静态局部变量。
* 堆：运行时动态分配的存储空间，比如malloc分配的内存
* 栈：栈包括函数调用的信息

下图是可执行程序四个区域的分布图：

![](0.6936750221061421-20220206105716-1sgrjlp.png)

这里面有几点需要说明：

1. 堆和栈的增长方向不同，栈的增长方向是从高地址向低地址增长，堆则相反
2. **栈顶**实际上市栈内存的底部。**栈底**实际上是栈内存的顶部。之所以上下颠倒是因为内存结构。
3. push指令将值压入栈顶，pop将值从栈顶弹出并放入寄存器或者你指定的位置

## 函数的组成部分

这一部分介绍函数的组成部分，主要是为了下文中和栈结合起来使用，虽然已经非常熟悉，但是还是要对其中的一些细节牢记：

```cpp
// 去掉了负数等临街情况的检查
int pow(int base, int ex) {
	int result = base;
	int i = 0;
	for(i = 0; i < ex; i++) {
		result *= base;
	}

	return result;
}
```

为了方便理解，以上是一段用C语言写的pow函数

* 函数名

函数名pow代表函数代码的起始位置。在汇编语言中类似于jmp跳转后面跟着的标签

* 参数

base和ex。这里面要知道这个参数指的是Parameter,也就是所谓的形参。而2,3则是Argument,也就是所谓的实参。

* 局部变量

局部变量(result, i)是函数内部进行计算和处理的时候使用的数据区，函数返回的时候就被丢弃

* 静态变量

这里面更多是指函数内部的静态变量，和局部变量不同，函数返回的时候，生命周期不会被销毁。类似static int count这类函数中的计数器。

* 全局变量

全局变量的生命周期在程序运行的时候一直都不销毁，所有函数都可以访问的数据区。

* 返回地址

返回地址作为参数告诉函数，当函数执行完毕以后应该再次从哪里开始。这是非常重要的概念。先看下面一小段代码：

```cpp
int main() {
	int result = pow(2, 3);
	printf("result = %d", result); // pow返回以后，从这开始调用printf
}
```

在这段函数中，main调用了pow,之后再调用printf。pow函数执行完毕以后把返回值和控制权都返回给main,main需要知道接下来从哪开始，为了让program flow在pow运行完毕以后执行接下来的printf,那么pow函数必须能够返回到调用它的地方。后面在函数与栈的结合的部分你会看到细节的部分。

虽然在以上的C语言代码中你"看不见"返回地址，但是编程语言在调用函数的时候会自动传递这个参数。

* 返回值

## 函数与栈

在简要介绍了函数和栈的工作原理以后，我们结合着两部分，讲一下函数的调用时如何与栈关联在一起的。

## call and ret

首先我们要引入汇编语言中关于函数调用的两个重要的指令：**call**和**ret**

* **call 标签**

call用于函数的调用。会执行两部分操作，首先将返回地址压入栈中，接着跳转到函数标签。和jmp指令不同的是，jmp只会跳转到相应的标签，而call把返回地址入栈以便函数返回。

* **ret**

ret把栈顶的值弹出，并将指令指针寄存器(%eip or %rip)的值设置为弹出值。程序根据指令指针寄存器执行接下来的命令。

我们通过下图来更加清晰明了的理解这个过程：

![](0.8882168560031158-20220206105716-zil8kz8.png)

首先栈顶为0x108,所以栈顶寄存器%esp为0x108.指令指针寄存器为0x804854e.

804854e的内存地址中对应的指令是call main,main函数的标签地址是0x8048b90.

执行call指令,根据定义首先把返回地址(下一条指令的地址0x8048553)入栈，栈顶向下移动4个byte(1个字=4字节),接着设置%eip为main函数的标签地址(0x8048b90)，跳转到这个地址开始执行main函数的逻辑。

最后main函数返回的时候，使用ret指令把栈顶的返回地址弹出到指令指针寄存器%eip,栈恢复调用前的样子，然后根据%eip程序往下执行(pushl %eax)

## 栈帧 stack frame

其次我们要讲解栈帧以及和栈帧相关的三个极其重要的寄存器。

栈(call stack)是由多个或者一个栈帧组成。你在其他书上看到的活跃记录也是栈帧的意思。后面的例子你将会看到如何使用寄存器构建栈帧。

通常来说栈帧包含以下元素：

* 参数
* 返回地址
* 函数内部的临时变量

三个重要的寄存器:

* 基址指针寄存器(%ebp, %rbp)

可以叫base pointer, frame pointer。使用基址寻址(base pointer addressing mode)可以通过固定的偏移量来访问函数的参数和局部变量。

* 栈寄存器(%esp, %rsp)

栈顶会随着push,pop这类指令上下移动，栈寄存器包含一个指向栈顶的指针。无论栈顶在哪里，我们都能通过栈寄存器轻易的访问到栈顶的位置。

* 指令指针寄存器(%eip, %rip)

program counter,用于指示程序应该从哪开始执行

## 实例

最后我们以函数pow为例子看一下汇编代码和栈之间的关联。

```nasm
# 计算2^3
.section .data
.section .text
.globl _start
_start:
pushl $3 #第二个参数3
pushl $2 #第一个参数2 参数按照逆序入栈
call power 
addl $8, %esp #将栈指针向上移动，销毁参数2和3的空间
movl %eax, %ebx #状态码只能保存在%ebx中，所以移动返回值到%ebx
movl $1, %eax #退出(返回%ebx)
int $0x80

.type power, @function
power:
pushl %ebp #保留旧基址指针
movl %esp, %ebp #将基址指针设为栈指针
subl $4, %esp #为本地存储保留空间

movl 8(%ebp), %ebx #底数
movl 12(%ebp), %ecx #指数
movl %ebx, -4(%ebp) #存储当前结果

power_loop_start:
cmp $1, %ecx #和1进行比较，如果指数是1，则我们已经得到结果
je end_power

movl -4(%ebp), %eax #将当前的计算结果从临时变量移入到%eax中
imull %ebx, %eax #当前结果与底数相乘
movl %eax, -4(%ebp) #将计算结果保存到临时变量中
decl %ecx #指数减去1
jmp power_loop_start 跳转到循环的开始，再次进行计算

end_power:
movl -4(%ebp), %eax #将函数的返回值保存到%eax中
movl %ebp, %esp 恢复栈指针，临时变量都被销毁
popl %ebp #恢复基址指针，这个时候栈帧已经被销毁
ret

# 编译 as -32 power.s -o power.o
# 链接 ld -melf_i386 power.o -o power 我的虚拟机是64位的，32位的可以去掉-melf_i386的选项
# 执行 ./power
# 获取状态码 echo $? 8

```

上面这段代码计算的就是2的3次方。 我们一步一步的分析和讲解整个程序。

首先从这段代码开始：

```nasm
pushl $3 #第二个参数3
pushl $2 #第一个参数2 参数按照逆序入栈
call power 
```

![](0.23189424034367226-20220206105716-tzmaz5v.png)

接下来跳转到power函数:

```text
.type power, @function
power:
pushl %ebp #保留旧基址指针
movl %esp, %ebp #将基址指针设为栈指针
subl $4, %esp #为本地存储保留空间
```

这一段语句就是用来创建栈帧的指令。

1. pushl %ebp指令保存当前的基址指针寄存器到栈中
2. movl %esp, %ebp把当前栈顶的值复制到%ebp中.这样通过基址寻址添加固定的偏移量就可以访问参数。为什么可以这样呢？因为把栈指针复制到基址指针以后，基址指针讲一直指向函数开始时的位置，向上是返回地址与参数，向下是临时变量。是对当前栈帧的常量引用。
3. subl $4, %esp.为函数的局部变量保留一个字(32位)存储空间。

执行以上命令以后，栈如下所示：

![](0.9871781568308677-20220206105716-0ffef2x.png)

之后进入计算逻辑

```nasm
movl 8(%ebp), %ebx #底数
movl 12(%ebp), %ecx #指数
movl %ebx, -4(%ebp) #存储当前结果

power_loop_start:
cmp $1, %ecx #和1进行比较，如果指数是1，则我们已经得到结果
je end_power

movl -4(%ebp), %eax #将当前的计算结果从临时变量移入到%eax中
imull %ebx, %eax #当前结果与底数相乘
movl %eax, -4(%ebp) #将计算结果保存到临时变量中
decl %ecx #指数减去1
jmp power_loop_start 跳转到循环的开始，再次进行计算
```

这一部分的逻辑比较简单，重点要明白-4(%ebp)是对临时变量的引用，其他的就好理解了。

最后函数执行完毕：

```nasm
end_power:
movl -4(%ebp), %eax #将函数的返回值保存到%eax中
movl %ebp, %esp 恢复栈指针，临时变量都被销毁
popl %ebp #恢复基址指针，这个时候栈帧已经被销毁
ret
```

ret执行以后，栈如下所示：

![](0.8668350059527821-20220206105716-pyy13m1.png)

重点是面的部分，当函数执行完毕的时候做以下3件事情：

1. 把返回值从临时变量复制到%eax
2. 恢复栈指针和基址指针。销毁当前的栈帧，包括当前栈帧的临时变量，并使得调用代码的栈帧重新生效。
3. ret指令返回值弹出栈顶到指令指针寄存器中，程序的控制权返回给调用它的函数。

如果不恢复栈帧，栈顶就不是返回地址，ret指令就无法把控制权交出去。

## 递归与栈

递归函数是上面部分的扩展，通过学习递归函数在递归和回溯的过程中栈的变化，可以加深你对函数和栈的理解。

以下是一段C语言的阶乘函数为了便于理解，去掉了临街条件的判断：

```text
int fact(int n) {
	if(n == 1) {
		return 1; #基线条件
	}else {
		return n * fact(n - 1); #递归部分
	}
}
```

下面我们通过计算阶乘函数来引入递归函数和栈的关系。汇编代码如下：

```cpp
.section .data
.section .text
.globl _start
.globl _factorial
_start:
pushl $4 
call factorial 
addl $4, %esp 
movl %eax, %ebx 
movl $1, %eax
int $0x80

.type factorial, @function
factorial:
pushl %ebp
movl %esp, %ebp #构建栈帧

movl 8(%ebp), %eax 
cmpl $1, %eax #基线条件，1的阶乘还是1
je end_factorial

decl %eax #递减参数
pushl %eax #将参数入栈

call factorial #调用函数

movl 8(%ebp), %ebx #把当前栈帧的参数移入%ebx
imull %ebx, %eax  #参数乘以fact函数的结果

end_factorial:
movl %ebp, %esp
popl %ebp #销毁当前栈帧，并激活上一个函数的栈帧
ret
```

我们还是按照上面的步骤画出代码对应的栈帧。

首先我们看一下函数的递推阶段(wind phase)

1. pushl $4 把4入栈
2. call factorial 调用阶乘函数，把返回地址入栈，设置指令指针寄存器，然后跳转到factorial标签所对应的地址。
3. pushl %ebp, movl %esp, %ebp 构建栈帧
4. movl 8(%ebp), %eax 根据基址指针的偏移(8(%ebp))把参数移动%eax中
5. cmpl $1, %eax , je end_factorial 检测基线条件，与1进行比较,相等则jmp到end_factorial,否则向下执行
6. decl %eax, pushl %eax,递减参数，将参数入栈
7. call factorial 最后再调用阶乘函数 直到满足基线条件

下图是栈帧的布局：

![](0.5755366848541726-20220206105716-opxt94s.png)

接下来我们看一下回溯的阶段(unwind phase)

1. 满足基线条件，跳转到end_factorial之后在当前栈销毁并根据返回地址激活上一个函数的栈帧。
2. 根据基址指针的偏移(8(%ebp))把参数移动到%ebx中，并用上一个栈帧的返回值%eax与%ebx相乘，并保存到%eax中。
3. 一直到栈帧回溯结束，%eax中为最终的阶乘函数的结果

回溯阶段是难点，因为call factorial之后的部分是在回溯阶段才执行，这块一定要清楚。

使用下图更方便你对回溯阶段加深理解：

![](0.06212470348992786-20220206105716-wz08er9.png)

## 尾递归

栈维护了每个函数调用的信息直到函数返回才会释放，如果递归的层级太深很容易造成stack overflow或者占用大量的内存空间。同时在生成和销毁栈帧的过程中有函数相关的信息需要保存和恢复，这样也会消耗很多的时间。 除了使用迭代以外还可以是用尾递归，让函数在回溯的过程中不需要做额外的计算和操作。 这部分可以作为扩展。

## 总结

以上就是函数与栈部分的内容。复杂度不是很高，主要是理解每一条指令对应的栈的变化，使用画图的方法来进行演示，会让你事半功倍。 文章中有错误和问题请及时支出，谢谢:)

## 引用

[Programming from the ground up](https://link.zhihu.com/?target=https%3A//book.douban.com/subject/25789594/) 一本讲汇编和体系结构不可多得的好书，短小精悍，配合着[csap](https://link.zhihu.com/?target=https%3A//book.douban.com/subject/26344642/)效果特别好。

[call stack](https://link.zhihu.com/?target=https%3A//en.wikipedia.org/wiki/Call_stack)

[Factorial Function in x86_64 assembly](https://link.zhihu.com/?target=https%3A//picoledelimao.github.io/blog/2016/02/21/factorial-function-in-assembly/)

[Journey to the stack](https://link.zhihu.com/?target=http%3A//cryptroix.com/2016/10/16/journey-to-the-stack/)
